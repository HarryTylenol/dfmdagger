/*
 * Copyright (C) 2019 Su Khai Koh
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${versions.dokka}"
    }
}

apply plugin: "org.jetbrains.dokka"
apply plugin: "com.github.dcendents.android-maven"

dokka {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

task javadocJar(type: Jar, dependsOn: dokka) {
    group = 'jar'
    description = 'Create java doc jar'

    archiveClassifier.set('javadoc')
    from "$buildDir/javadoc"
}

task sourcesJar(type: Jar) {
    group = 'jar'
    description = 'Create sources jar'

    archiveClassifier.set('sources')
    from android.sourceSets.main.kotlin.srcDirs
}

task classesJar(type: Jar) {
    group = 'jar'
    description = 'Create classes jar'

    from "$buildDir/tmp/kotlin-classes/release"
}

artifacts {
    archives classesJar
    archives javadocJar
    archives sourcesJar
}

dependencies {
    dokkaRuntime "org.jetbrains.dokka:dokka-fatjar:${versions.dokka}"
}