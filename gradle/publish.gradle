/*
 * Copyright (C) 2019 Su Khai Koh
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:${versions.dokka}"
    }
}

apply plugin: "org.jetbrains.dokka-android"
apply plugin: "com.github.dcendents.android-maven"

repositories {
    maven { url "https://jitpack.io" }
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    group = 'jar'
    description = 'Create java doc jar'

    archiveClassifier.set('javadoc')
    from "$buildDir/javadoc"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

task classesJar(type: Jar) {
    from "$buildDir/intermediates/classes/release"
}

github {
    owner = getEnvProperty("GITHUB_OWNER")
    repo = getEnvProperty("GITHUB_REPO")
    token = getEnvProperty("GITHUB_TOKEN")
    tagName = getEnvProperty("ARTIFACT_VERSION")
    targetCommitish = getEnvProperty("TARGET_COMMITISH")
    name = getEnvProperty("ARTIFACT_VERSION")
    body = getEnvProperty("RELEASE_NOTES")
}

artifacts {
    archives classesJar
    archives javadocJar
    archives sourcesJar
}

dependencies {
    dokkaRuntime "org.jetbrains.dokka:dokka-fatjar:${versions.dokka}"
}